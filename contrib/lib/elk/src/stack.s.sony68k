/* int stksize();
 * int saveenv(char* envbuf);
 * dead jmpenv(const char* envbuf, int retcode);
 */
	.globl	_stksize
	.globl	_Special
_stksize:
	move.l	_stkbase,d0
	sub.l	sp,d0
	add.l	#120,d0
	rts

	.globl	_saveenv
_saveenv:
	move.l	(4,sp),a0
	move.l	a6,(12,a0)		/* save frame pointer of caller */
	move.l	(sp)+,a1		/* pop return address */
	move.l	a1,(4,a0)		/* save pc of caller */
	move.l	sp,(8,a0)
	movem.l	#0xBCFC,(40,a0)		/* XXX (shouldn't need this) XXX */
	move.l	sp,a2
	move.l	_stkbase,a3
	move.l	a0,a4
	add.l	#110,a4
rep1:	move.l	(a2)+,(a4)+
	cmp.l	a2,a3
	jbcc	rep1
	move.l	a4,d0			/* New pointer */
	sub.l	a2,d0			/* Minus old pointer */
	move.l	d0,(a0)    		/* is the relocation offset */
	movem.l	(40,a0),#0xBCFC		/* XXX (shouldn't need this) XXX */
	move.l	_Special,d0
	jmp	(a1)

	.globl	_jmpenv
_jmpenv:
	move.l	(8,sp),d0		/* return value */
	move.l	(4,sp),a0		/* fetch buffer */

	move.l	(8,a0),sp
	move.l	sp,a2
	move.l	_stkbase,a3
	move.l	a0,a4
	add.l	#110,a4
rep2:	move.l	(a4)+,(a2)+
	cmp.l	a2,a3
	jbcc	rep2
	movem.l	(40,a0),#0xBCFC		/* XXX (shouldn't need this) XXX */
	move.l	(12,a0),a6		/* restore frame pointer */
	move.l	(4,a0),a1		/* pc */
	jmp	(a1)
